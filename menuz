#!/usr/bin/env zsh

#
# menus with rofi\fzf and zsh arrays
#

typeset menudir=${MENUZ_DIR:-$HOME/.config/menuz}

# check for rofi
(( $+commands[rofi] )) || \
  { echo "Could not locate rofi!"; exit 1 }

# check for fzf
(( $+commands[fzf] )) || \
  { echo "Could not locate fzf!"; exit 1 }

# check for $menudir
[[ -d $menudir ]] || \
  { echo "$menudir does not exist!"; exit 1 }

# build menu
function _show_menu() {
  typeset NAME
  typeset -A ITEMS
  local menu=$menudir/$1
  local choice

  # display specified menu
  if [[ -f $menu ]]
  then
    source $menu

    choice=$( ( _get_items ${(k)ITEMS[@]}) | eval "$(_get_ui $NAME)")
    if [[ -n $choice ]]
    then
      eval ${ITEMS[$choice]}
    fi

  # select menu to edit
  elif [[ $1 == "-e" ]]
  then
    _edit_menu

  # select menu to display
  else
    _select_menu
  fi
}

# select menu
function _select_menu() {
  typeset -A LIST
  typeset NAME
  typeset -A ITEMS
  local menu
  local choice
  local title=${MENUZ_LABEL_SELECT:-"Select Menu"}

  for menu ($menudir/*)
  do
    source $menu
    LIST[$NAME]+=$(basename $menu)
  done

  choice=$( (_get_items ${(k)LIST[@]}) | eval "$(_get_ui $title)")
  if [[ -n $choice ]]
  then
    _show_menu ${LIST[$choice]}
  fi
}

# Edit
function _edit_menu() {
  typeset -A LIST
  typeset NAME
  typeset -A ITEMS
  local menu
  local choice
  local title=${MENUZ_LABEL_EDIT:-"Edit Menu"}

  for menu ($menudir/*)
  do
    source $menu
    LIST[$NAME]+=$menu
  done

  choice=$( (_get_items ${(k)LIST[@]}) | eval "$(_get_ui $title)")
  if [[ -n $choice ]]
  then
    exec $TERMINAL -e $EDITOR ${LIST[$choice]}
  fi
}

# Print menu items
function _get_items() { local i; for i ($@); do echo $i; done }

# Determine if script was called from terminal or GUI
# and return appropriate UI
function _get_ui() {
  local proc=$(readlink -f /proc/$(ps -o ppid:1= -p $$)/exe)
  local shell=$(readlink -f "$SHELL")
  if [[ $proc == $shell ]] || [[ ${proc:t} == "tmux" ]]
  then
    echo "fzf -e --layout=reverse --header='$@'"
  else
    echo "rofi -dmenu -i -p '$@'"
  fi
}

_show_menu $@

exit 0

# vim: ft=sh ts=2 sw=0 et:
